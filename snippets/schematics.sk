# Description: Efficiently create and paste schematics using FAWE, rather than vanilla worldedit, or structures.
# Authors: Boxic
# Dependencies: Skript-Reflect (https://github.com/TPGamesNL/skript-reflect/releases/), Fast Async World Edit (https://intellectualsites.github.io/download/fawe.html)
# Example Usage: Schematic Creation > create schematic "plugins/FastAsyncWorldEdit/schematics/%loop-index%.schem" from pos1 {_pos1} pos2 {_pos2} origin {_loc-origin}
# Example Usage: Schematic Pasting > paste schematic "plugins/FastAsyncWorldEdit/schematics/%loop-index%.schem" at {_loc-origin} include air true include entities false


import:
    com.sk89q.worldedit.bukkit.BukkitWorld
    com.sk89q.worldedit.extent.clipboard.Clipboard
    com.sk89q.worldedit.extent.clipboard.io.ClipboardFormats
    com.sk89q.worldedit.math.BlockVector3
    com.sk89q.worldedit.regions.CuboidRegion
    java.io.File
    java.nio.file.Files
    java.nio.file.Paths


# Pastes a schematic from a file path to a location, optionally include/exclude air and entities
effect paste schematic %string% at %location% include air %boolean% include entities %boolean%:
    trigger:
        set {_file} to new File(expression-1)
        set {_location} to BlockVector3.at(expression-2.getBlockX(), expression-2.getBlockY(), expression-2.getBlockZ())
        set {_world} to new BukkitWorld(expression-2.getWorld())
        ClipboardFormats.findByFile({_file}).load({_file}).paste({_world}, {_location}, false, expression-3, expression-4, null)


# Creates a schematic between two locations from an origin location, and saves to a file path
effect create schematic %string% from pos1 %location% pos2 %location% origin %location%:
    trigger:
        Files.deleteIfExists(Paths.get(expression-1))
        set {_file} to new File(expression-1)
        set {_pos1} to BlockVector3.at(expression-2.getBlockX(), expression-2.getBlockY(), expression-2.getBlockZ())
        set {_pos2} to BlockVector3.at(expression-3.getBlockX(), expression-3.getBlockY(), expression-3.getBlockZ())
        set {_origin} to BlockVector3.at(expression-4.getBlockX(), expression-4.getBlockY(), expression-4.getBlockZ())
        set {_world} to new BukkitWorld(expression-2.getWorld())
        # Define selection region and copy it to clipboard
        set {_region} to new CuboidRegion({_world}, {_pos1}, {_pos2})
        set {_clipboard} to Clipboard.create({_region})
        # Set the origin of copied selection
        {_clipboard}.setOrigin({_origin})
        # Save selection as schematic
        {_clipboard}.save({_file}, ClipboardFormats.findByFile({_file}))
