# Description: Create a book with a certain title, author, and text on pages (ALLOWING COMPONENTS)
# Authors: GStudiosX
# Dependencies: Skript-Reflect (https://github.com/TPGamesNL/skript-reflect/releases/), SkBee (https://github.com/ShaneBeee/SkBee/releases)
# Example Usage: set {_t::1} to text component of "&b&lRULES"
#                set hover event of {_t::1} to hover event showing "make sure to read our rules"
#                set click event of {_t::1} to click event to run command "/rules"
#                set {_t::2} to text component of "%nl%&b&ltest"
#                add merge components {_t::*} to {_pages::*}
#
#                set {_t2::1} to text component of "&b&lTest"
#                set {_t2::2} to text component of "%nl%&b&ltest"
#                add merge components {_t2::*} to {_pages::*}
#
#                give createBook("&bTest", "", {_pages::*}) to player


import:
    org.bukkit.inventory.meta.BookMeta$Generation

function getGenerationType(type: string) :: object:
    return Generation.COPY_OF_COPY if ("copy_of_copy" and "copied_copy") contains {_type}.toLowerCase()
    return Generation.COPY_OF_ORIGINAL if ("copy" and "copy_of_original") contains {_type}.toLowerCase()
    return Generation.ORIGINAL

function createBook(title: string, author: string, pages: text components, generationType: string = "original") :: item:
    set {_book} to written book named {_title}
    set book author of {_book} to {_author}
    set book title of {_book} to {_title}
    loop {_pages::*}:
        set page loop-index parsed as number of book {_book} to loop-value
    set {_itemMeta} to {_book}.getItemMeta()
    {_itemMeta}.setGeneration(getGenerationType({_generationType}))
    {_book}.setItemMeta({_itemMeta})
    return {_book}